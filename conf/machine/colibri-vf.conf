#@TYPE: Machine
#@NAME: colibri_vf50/colibri_vf61 SOM powered by Freescale Vybrid family
#@SOC: vf50 vf61
#@DESCRIPTION: Toradex Colibri VF50, Colibri VF61

# meta-angstrom/conf/distro/include/arm-defaults.inc
DEFAULTTUNE = "armv7ahf-neon"
require conf/machine/include/imx-base.inc
require conf/machine/include/tune-cortexa5.inc

SOC_FAMILY = "vf:vf60"

PREFERRED_PROVIDER_virtual/kernel ?= "linux-toradex-vf"
KERNEL_IMAGETYPE = "zImage"
KERNEL_DEVICETREE += "vf500-colibri-eval-v3.dtb vf610-colibri-eval-v3.dtb"

# U-Boot reads the kernel and device tree directly from rootfs
IMAGE_INSTALL_append = " kernel-image kernel-devicetree"

PREFERRED_PROVIDER_u-boot = "u-boot-toradex-fsl"
PREFERRED_PROVIDER_virtual/bootloader = "u-boot-toradex-fsl"
UBOOT_MACHINE = "colibri_vf_defconfig"
UBOOT_SUFFIX = "imx"
UBOOT_MAKE_TARGET = ""

#All other Toradex modules require version 1.14, so stay with it also with the VF modules
PREFERRED_PROVIDER_virtual/xserver_vf = "xserver-xorg"
PREFERRED_VERSION_xserver-xorg_vf = "1.14%"

# note that this is a pure SW solution, don't expect GPU performance
PREFERRED_PROVIDER_virtual/mesa = "mesa"
PREFERRED_PROVIDER_virtual/gl = "mesa"
PREFERRED_PROVIDER_virtual/libgl = "mesa"

SDCARD_ROOTFS ?= "${DEPLOY_DIR_IMAGE}/${IMAGE_NAME}.rootfs.ext3"
IMAGE_FSTYPES ?= "tar.bz2 ext3 sdcard"

SERIAL_CONSOLES = "115200;ttyLP0 115200;ttyGS0"

MACHINE_FEATURES = "screen usbgadget usbhost vfat alsa touchscreen wifi bluetooth 3g"
EXTRA_IMAGEDEPENDS += "u-boot"

#Workaround
#if there is /dev/null with correct major and minor number, the userspace RS232
#console gobbles most of the output. Sometimes there is a kernel oops during
#init. So don't provide any device files during in the rootfs and let the
#tempdevfs do the work
USE_DEVFS = "1"
